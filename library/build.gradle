import com.android.tools.lint.EcjParser

long expectedValue = EcjParser.getLanguageLevel(1, 8)
EcjParser.metaClass.'static'.getLanguageLevel = { int i, int j -> expectedValue }

apply plugin: 'com.android.library'

apply plugin: 'me.tatarka.retrolambda'

apply plugin: 'idea'
idea {

    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group = 'net.sf.fdshare'
version = '0.2.2'

android {
    compileSdkVersion 21 // mainly for try-with-resources
    buildToolsVersion '22.0.1'

    publishNonDefault !uploadInitiated

    sourceSets.main.jni.srcDirs = []

    defaultConfig {
        minSdkVersion 8
        //noinspection GradleDependency
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            jniDebuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }

    packagingOptions {
        pickFirst 'LICENSE.txt'
        pickFirst 'META-INF/ASL2.0'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/maven/*'
    }
}


import org.apache.tools.ant.taskdefs.condition.Os

def buildCmd = Os.isFamily(Os.FAMILY_WINDOWS) ? 'ndk-build.cmd' : 'ndk-build'

tasks.addRule("NDK tasks") { String taskName ->
    def matcher = taskName =~ /fdshareBuildPie(.+)/

    if (matcher) {
        boolean usePie = matcher.group(1) == 'Enabled'

        task(type: Exec, taskName) {
            doFirst {
                exec {
                    commandLine buildCmd, '-C', "$projectDir.absolutePath/src/main/", 'clean'
                }
            }

            def flag = usePie ? 'LOCAL_CFLAGS=-DFDSHARE_LIBRARY' : 'LOCAL_CFLAGS=-DNO_PIC'

            commandLine buildCmd, '-C', "$projectDir.absolutePath/src/main/", "APP_PIE=$usePie", flag

            doLast {
                project.copy {
                    from "$projectDir.absolutePath/src/main/libs"
                    into "$projectDir.absolutePath/src/main/jniLibs"
                    include '**/*'
                    exclude '**/*.jar'
                    exclude '**/*.so'
                    rename(/(.+)/, usePie ? 'lib$1_PIC_exec.so' : 'lib$1_exec.so')
                }
            }
        }
    }
}

tasks.withType(JavaCompile).all {
    it.dependsOn fdshareBuildPieEnabled, fdshareBuildPieDisabled
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.convention
}

dependencies {
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:1.8.0'

    compile 'com.android.support:support-annotations:22.2.0'

    androidTestCompile 'junit:junit:4.11'
    androidTestCompile('com.android.support.test:testing-support-lib:0.1') {
        exclude group: 'junit'  // junit:junit-dep conflicts with junit:unit
    }
}

def maintGradleScript = file("$rootProject.projectDir/deployment.gradle")
if (maintGradleScript.exists()) {
    apply from: maintGradleScript
}
